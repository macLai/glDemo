<html> 
<head> 
<title>webgl</title> 
<meta http-equiv="content-type" content="text/html; charset=ISO-8859-1"> 

<script src="glm-js.min.js"></script>
<script src="gl.js"></script>

<script id="shader-vs-sun" type="x-shader/x-vertex"> 
    attribute vec3 positionAttr;

    uniform mat4 project;
    uniform mat4 view;
    uniform mat4 model;
 
    void main(void) {
        gl_Position = project * view * model * vec4(positionAttr, 1.0);
    }
</script> 
<script id="shader-vs-item" type="x-shader/x-vertex"> 
    attribute vec3 positionAttr;
    attribute vec3 normalVecAttr;

    uniform mat4 project;
    uniform mat4 view;
    uniform mat4 model;
    uniform mat4 modelTransformed;
    uniform mat4 lightSpaceMatrix;

    varying vec3 vNormal;
    varying vec3 vPos;
    varying vec3 vPosShadow;
 
    void main(void) {
        vPos = (model * vec4(positionAttr, 1.0)).xyz;
        vNormal = mat3(modelTransformed) * normalVecAttr;
        vec4 shadow = lightSpaceMatrix * vec4(vPos, 1.0);
        vPosShadow = shadow.xyz / shadow.w;
        
        gl_Position = project * view * vec4(vPos, 1.0);
    }
</script> 
<script id="shader-vs-shadow" type="x-shader/x-vertex"> 
    attribute vec3 positionAttr;

    uniform mat4 lightSpaceMatrix;
    uniform mat4 model;
 
    void main(void) {
        gl_Position = lightSpaceMatrix * model * vec4(positionAttr, 1.0);
    }
</script> 
<script id="shader-fs-sun" type="x-shader/x-fragment"> 
    precision mediump float;

    void main(void) {
        gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
    }
</script> 
<script id="shader-fs-item" type="x-shader/x-fragment"> 
    precision mediump float;

    uniform sampler2D uShadowImage;

    varying vec3 vNormal;
    varying vec3 vPos;
    varying vec3 vPosShadow;

    uniform vec3 lightPos;
    uniform vec3 viewPos;

    void main(void) {
        vec3 lightColor = vec3(1.0, 1.0, 1.0);
        vec3 objectColor = vec3(0.5, 0.1, 0.0);

        float ambientStrength = 0.3;
        vec3 ambient = ambientStrength * lightColor;

        vec3 lightDir = normalize(lightPos - vPos);
        vec3 normalDir = normalize(vNormal);
        float diff = max(dot(normalDir, lightDir), 0.0);
        vec3 diffuse = diff * lightColor;

        float specularStrength = 0.5;
        vec3 viewDir = normalize(viewPos - vPos);
        vec3 reflectDir = reflect(-lightDir, normalDir);
        float spec = pow(max(dot(viewDir, reflectDir), 0.0), 32.0);
        vec3 specular = specularStrength * spec * lightColor;

        vec3 shadowCoords = vPosShadow * 0.5 + 0.5;
        float closestDepth = texture2D(uShadowImage, shadowCoords.xy).r; 
        float currentDepth = shadowCoords.z;
        float bias = max(0.05 * (1.0 - dot(normalDir, lightDir)), 0.005);

        float shadow = 0.0;
        vec2 texelSize = 1.0 / vec2(1024.0, 1024.0);
        for(int x = -1; x <= 1; ++x) {
            for(int y = -1; y <= 1; ++y) {
                float pcfDepth = texture2D(uShadowImage, shadowCoords.xy + vec2(x, y) * texelSize).r; 
                shadow += currentDepth - bias > pcfDepth ? 1.0 : 0.0;        
            }    
        }
        shadow /= 9.0;

        vec3 result = (ambient + (1.0 - shadow) * (diffuse + specular)) * objectColor;
        gl_FragColor = vec4(result, 1.0);
    }
</script> 
<script id="shader-fs-shadow" type="x-shader/x-fragment"> 
    precision mediump float;

    void main(void) {
    }
</script> 

</head> 
 
<body onload="webGLStart();"> 
    <canvas id="can" style="border: none;" width="500" height="500"></canvas> 
</body> 
 
</html>
